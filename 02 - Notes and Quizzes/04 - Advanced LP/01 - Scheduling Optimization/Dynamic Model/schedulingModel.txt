#AMPL code for the "Macrosoft" problem DSA/ISE 5113 

reset;

#OPTIONS ---------------------------------------

options solver cplex;

#PARAMETERS AND SETS --------------------------

set T circular;    #set of time periods

param c_FT   >=0;  #cost of FT employee
param c_PT   >=0;  #cost of PT employee

param PT_FTE >=0;  #value of PT employee
param HRfraq >=0;  #HR requirement of min percent of FT employees working

param FTE{T} >=0;  #daily FTE requirements 

#DECISION VARIABLES ----------------------------
var x{T} >=0 integer; #number of FT employees starting in each time period
var y{T} >=0 integer; #number of PT employees starting in each time period

#comment-----
# note if you wanted to model this 'correctly', it should be an integer programming problem
# to make the decision variables integer, just add the keyword to the definition.
# That is, replace the two 'var' declaration lines above with these lines:
# var x{T} >=0 integer; 
# var y{T} >=0 integer; 


#OBJECTIVE --------------------------------------
minimize cost: c_FT*sum{t in T}x[t] + c_PT*sum{t in T}y[t];

#CONSTRAINTS ------------------------------------
subject to FTEreq {t in T}: x[t] + x[prev(t)] + PT_FTE*y[t] >= FTE[t];
subject to HRreq  {t in T}: x[t] + x[prev(t)] >= HRfraq * (x[t] + x[prev(t)] + y[t]);

#DATA ------------------------------------------
data models/macrosoft-2.dat;

#COMMANDS --------------------------------------
solve;
display x;
display y;